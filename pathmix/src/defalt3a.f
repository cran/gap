      SUBROUTINE DEFALT3a(NALL,XINIT,LFNTER,LFNPLX)
C---
C--- INITIALIZES PARAMETER VALUES TO DEFAULTS
C---
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)

      DIMENSION   XINIT(NALL)

#include "path3.inc"

      CHARACTER   GRPNAM(NGROUP)*2
      DIMENSION   NN(NGROUP), MXSZ(NGROUP), NRHO(NGROUP,NGROUP)

      DATA GRPNAM /'PF','IF','PM','IM','PC','IC'/

C MAKE SURE FAMILY DATA EXISTS

      WRITE (LFNTER,1010) NFAM
 1010 FORMAT ( 10X, 'NO. OF FAMILIES    =', I7 )

      IF ( NFAM .LT. 1 ) THEN
         call intpr('NO DATA',-1,0,0)
         STOP
      END IF

C GET MEAN, VARIANCE AND CORRELATION ESTIMATES

      I1 = 1
      I2 = NGROUP + 1

      ISTAN = 0
      CALL MERLIN(NGROUP,XINIT(I1),XINIT(I2),NN,MXSZ,RHO,NRHO,NGROUP,
     +   ISTAN,NFAM,NLINK,XOBS,NGROUP,MAXFAM)

C WRITE MEANS, VARIANCES, STARTING CORRELATIONS TO TERSE

      I1 = 0
      I2 = NGROUP

      WRITE (LFNTER,1100) (GRPNAM(I),   I=1,NGROUP)
      WRITE (LFNTER,1110) (XINIT(I1+I), I=1,NGROUP)
      WRITE (LFNTER,1120) (XINIT(I2+I), I=1,NGROUP)
      WRITE (LFNTER,1130) (NN(I),       I=1,NGROUP)
      WRITE (LFNTER,1140) (MXSZ(I),     I=1,NGROUP)

 1100 FORMAT (//' ','          ',6(9X,A3,1X))
 1110 FORMAT ( /' ','MEAN:     ',6F13.5)
 1120 FORMAT ( /' ','VARIANCE: ',6F13.5)
 1130 FORMAT ( /' ','NO. OBS.: ',6I13)
 1140 FORMAT ( /' ','MAX / FAM:',6I13)

      WRITE (LFNTER,1200)
      WRITE (LFNTER,1210) (GRPNAM(I), I=1,NGROUP)
      DO 220 I=1,NGROUP
         WRITE (LFNTER,1220) GRPNAM(I), (RHO(J,I), J=1,I)
220   CONTINUE

      WRITE (LFNTER,1230)
      WRITE (LFNTER,1240) (GRPNAM(I), I=1,NGROUP)
      DO 250 I=1,NGROUP
         WRITE (LFNTER,1250) GRPNAM(I), (NRHO(J,I), J=1,I)
250   CONTINUE

 1200 FORMAT (//' ',17X,19X,'APPROXIMATE CORRELATIONS')
 1210 FORMAT ( /' ',17X,3X,6(6X,A3,1X))
 1220 FORMAT ( /' ',17X,A3,6F10.5)

 1230 FORMAT (//' ',17X,19X,'APPROXIMATE SAMPLE SIZES')
 1240 FORMAT ( /' ',17X,3X,6(7X,A3))
 1250 FORMAT ( /' ',17X,A3,6I10)

C UNPACK CORRELATIONS INTO XINIT()

      K = 2 * NGROUP

      DO 320 I=1,NGROUP
         DO 310 J=I+1,NGROUP
            K = K + 1
            XINIT(K) = RHO(I,J)
310      CONTINUE
320   CONTINUE

      K = K + 1
      XINIT(K) = RHO(5,5)
      K = K + 1
      XINIT(K) = RHO(6,6)
      K = K + 1
      XINIT(K) = RHO(6,5)
      IF (K .NE. NALL) THEN
         call intpr('DEFALT: INTERNAL ERROR',-1,0,0)
         STOP
      END IF

C INITIALIZE GOODNESS OF FIT OUTPUT

      GOFOUT = .FALSE.
      LFNGOF = LFNPLX
      END
